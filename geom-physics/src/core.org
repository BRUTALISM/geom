#+SETUPFILE: ../../src/setup.org

* Contents                                                         :toc_3_gh:
 - [[#namespace-thinggeomphysicscore][Namespace: thi.ng.geom.physics.core]]
     - [[#protocols][Protocols]]
     - [[#type-implementations][Type implementations]]
     - [[#constructors][Constructors]]
     - [[#complete-namespace-definition][Complete namespace definition]]

* Namespace: thi.ng.geom.physics.core

** Protocols

#+BEGIN_SRC clojure :noweb-ref protos
(defprotocol PUpdate
  (update [_]))

(defprotocol PBehavioral
  (add-behavior [_ id b])
  (remove-behavior [_ id])
  (clear-behaviors [_])
  (apply-behaviors [_]))

(defprotocol PConstrained
  (add-constraint [_ id c])
  (remove-constraint [_ id])
  (clear-constraints [_])
  (apply-constraints [_]))

(defprotocol PParticle
  (add-force [_ f])
  (add-velocity [_ v])
  (apply-force [_])
  (clear-force [_])
  (clear-velocity [_])
  (scale-velocity [_ v])
  (position [_])
  (set-position [_ p])
  (velocity [_])
  (lock [_])
  (unlock [_])
  (locked? [_]))

(defprotocol PPhysics
  (update-particles [_])
  (update-springs [_]))
#+END_SRC

** Type implementations

#+BEGIN_SRC clojure :noweb-ref impl
  (defn apply-fns!
    [fns src]
    (loop [fns (seq fns), coll (seq src)]
      (if fns
        (if coll
          (do ((first fns) (first coll))
              (recur fns (next coll)))
          (recur (next fns) (seq src))))))

  (deftype VerletParticle
      ,#+clj
    [^:unsynchronized-mutable pos
     ^:unsynchronized-mutable prev
     ^:unsynchronized-mutable force
     ^:unsynchronized-mutable locked?
     ^:unsynchronized-mutable behaviors
     ^:unsynchronized-mutable constraints
     ^double weight
     ^double inv-weight]
    ,#+cljs
    [^:mutable pos
     ^:mutable prev
     ^:mutable force
     ^:mutable locked?
     ^:mutable behaviors
     ^:mutable constraints
     weight
     inv-weight]
    PBehavioral
    (add-behavior
      [_ id b] (set! behaviors (assoc (or behaviors {}) id b)) _)
    (apply-behaviors
      [_]
      (if-not locked?
        (loop [b (vals behaviors)] (when b ((first b) _) (recur (next b)))))
      _)
    (remove-behavior
      [_ id] (set! behaviors (dissoc behaviors id)) _)
    PConstrained
    (add-constraint
      [_ id c] (set! constraints (assoc (or constraints {}) id c)) _)
    (apply-constraints
      [_]
      (if-not locked?
        (loop [c (vals constraints)] (when c ((first c) _) (recur (next c)))))
      _)
    (remove-constraint
      [_ id] (set! constraints (dissoc constraints id)) _)
    PParticle
    (lock
      [_] (set! locked? true) _)
    (unlock
      [_] (set! locked? false) _)
    (locked?
      [_] locked?)
    (position
      [_] pos)
    (set-position
      [_ p] (set! pos p) _)
    (velocity
      [_] (g/- pos prev))
    (add-force
      [_ f] (set! force (g/+ force f)) _)
    (clear-force
      [_] (set! force (g/clear* force)))
    (apply-force
      [_]
      (let [p' pos]
        (set! pos (-> force
                      (g/madd weight (g/- pos prev))
                      (g/+ pos)))
        (set! prev p')
        (set! force (g/clear* force))
        _))
    (scale-velocity
      [_ s] (set! prev (g/mix prev pos s)) _)
    PUpdate
    (update
      [_] (if locked? _ (-> _ apply-behaviors apply-force apply-constraints)))
    Object
    (toString
      [_]
      (pr-str
       {:pos pos
        :prev prev
        :force force
        :locked? locked?
        :weight weight
        :behaviors behaviors
        :constraints constraints})))

  (defrecord VerletPhysics
      [particles springs behaviors constraints timestep delta iter drag]
    PBehavioral
    (add-behavior
      [_ id b] (assoc-in _ [:behaviors id] (b delta)))
    (apply-behaviors
      [_] (apply-fns! (vals behaviors) particles) _)
    (remove-behavior
      [_ id] (update-in [:behaviors] dissoc id))
    PConstrained
    (add-constraint
      [_ id c] (assoc-in _ [:constraints id] (c delta)))
    (apply-constraints
      [_] (apply-fns! (vals constraints) particles) _)
    (remove-constraint
      [_ id] (update-in [:constraints] dissoc id))
    PPhysics
    (update-particles
      [_]
      (loop [ps particles]
        (when ps
          (-> ps first (scale-velocity drag) update)
          (recur (next ps))))
      _)
    (update-springs
      [_] (loop [s springs] (when s (update (first s)) (recur (next s)))) _)
    PUpdate
    (update
      [_]
      (loop [i iter]
        (when (pos? i)
          (-> (apply-behaviors _)
              (update-particles)
              (update-springs)
              (apply-constraints))
          (recur (dec i))))
      _))

  (defrecord VerletSpring
      [^VerletParticle a
       ^VerletParticle b
       ^double rlen
       ^double strength
       a-locked? b-locked?]
    PUpdate
    (update
      [_]
      (let [aw    (.-inv-weight a)
            bw    (.-inv-weight b)
            pa    (position a)
            pb    (position b)
            delta (g/- pb pa)
            dist  (+ (g/mag delta) 1e-6)
            nd    (* (/ (- dist rlen) (* dist (+ aw bw))) strength)]
        (when-not (or a-locked? (locked? a))
          (set-position a (g/madd delta (* nd aw) pa)))
        (when-not (or  b-locked? (locked? b))
          (set-position b (g/madd delta (* (- nd) bw) pb))))))

  (defn gravity
    [force]
    (fn [delta]
      (let [f-scaled (g/* force (* delta delta))]
        #(add-force % f-scaled))))

  (defn attract
    [pos r strength]
    (let [r2 (* r r)]
      (fn [delta]
        (let [strength' (* strength delta)]
          (prn strength' delta)
          #(let [d (g/- pos (position %))
                 l (g/mag-squared d)]
             (if (< l r2)
               (add-force % (g/* d (/ (* (- 1.0 (/ l r2)) strength')
                                      (Math/sqrt l))))))))))
#+END_SRC

** Constructors

#+BEGIN_SRC clojure :noweb-ref ctors
  (defn particle
    ([pos weight]
       (particle pos weight false))
    ([pos weight lock?]
       (VerletParticle. pos pos (g/clear* pos) lock? nil nil weight (/ 1.0 weight))))

  (defn physics
    [{:keys [particles springs behaviors constraints timestep iter drag]
      :or   {particles #{}, springs #{}, behaviors {} constraints {}
             timestep 1.0 iter 10, drag 0.05}}]
    (let [delta (/ timestep iter)]
      (map->VerletPhysics
       {:particles   (set particles)
        :springs     (set springs)
        :behaviors   (reduce-kv (fn [acc k v] (assoc acc k (v delta))) behaviors behaviors)
        :constraints (reduce-kv (fn [acc k v] (assoc acc k (v delta))) constraints constraints)
        :timestep    timestep
        :iter        iter
        :delta       delta
        :drag        (* drag delta)})))
#+END_SRC

** Complete namespace definition                                   :noexport:

#+BEGIN_SRC clojure :tangle ../babel/src/cljx/thi/ng/geom/physics/core.cljx :noweb yes :mkdirp yes :padline no
  (ns thi.ng.geom.physics.core
    ,#+cljs (:require-macros [thi.ng.macromath.core :as mm])
    (:require
     [thi.ng.geom.core :as g]
     [thi.ng.geom.core.vector :as v :refer [vec2 vec3]]
     [thi.ng.common.data.core :as d]
     ,#+clj [thi.ng.macromath.core :as mm]))

  <<protos>>

  <<impl>>

  <<ctors>>
#+END_SRC

#+BEGIN_SRC clojure
  (require '[thi.ng.geom.physics.core :as ph] :reload)
  (def a (ph/particle (v/vec2) 1 true))
  (def b (ph/particle (v/vec2 1 0) 1))
  (def s (ph/map->VerletSpring {:a a :b b :rlen 10.0 :strength 0.01}))
  (def p (ph/physics {:particles [a b] :springs [s] :iter 10 :drag 0.0
                      :behaviors {:g (ph/gravity (v/vec2 0 -0.1))
                                  :a (ph/attract (v/vec2 10 0) 10 -2)}}))
  (dotimes [i 100] (ph/update p) (let [pa (ph/position a) pb (ph/position b)] (prn i pb (g/dist pa pb))))

#+END_SRC
