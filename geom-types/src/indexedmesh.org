#+SETUPFILE: ../../src/setup.org

* Contents                                                         :toc_4_gh:
 - [[#thinggeomindexedmesh][thi.ng.geom.indexedmesh]]
     - [[#helpers][Helpers]]
         - [[#constructors][Constructors]]
         - [[#attributes][Attributes]]
     - [[#protocol-implementations][Protocol implementations]]
         - [[#parea][PArea]]
         - [[#pbounds][PBounds]]
         - [[#pboundingsphere][PBoundingSphere]]
         - [[#pcenter][PCenter]]
         - [[#pflip][PFlip]]
         - [[#pgeomcontainer][PGeomContainer]]
         - [[#pvertexaccess-pedgeaccess--pfaceaccess][PVertexAccess, PEdgeAccess & PFaceAccess]]
         - [[#pnormalaccess][PNormalAccess]]
         - [[#todo-ptessellate][TODO PTessellate]]
         - [[#ptransform][PTransform]]
         - [[#pvolume][PVolume]]
         - [[#end-of-implementation][End of implementation]]
     - [[#complete-namespace-definition][Complete namespace definition]]

* thi.ng.geom.indexedmesh

** Helpers

*** Constructors

#+BEGIN_SRC clojure :noweb-ref ctors
  (defn indexed-mesh
    [] (thi.ng.geom.types.IndexedMesh. (idx/monotonic-index) [] {}))
#+END_SRC

*** Attributes

#+BEGIN_SRC clojure :noweb-ref impl
  (defn- add-face*
    [mesh [verts attribs]]
    #_(prn :add-face verts :attr attribs)
    (let [[vindex vids] (idx/index-coll (:vertices mesh) verts)
          [aindex aids] (idx/index-attribs (:attribs mesh) attribs)]
      (thi.ng.geom.types.IndexedMesh.
       vindex
       (conj (:faces mesh) (thi.ng.geom.meshface.IndexedMeshFace. vids aids))
       aindex)))

  (defn- flip-face*
    [[verts attribs]]
    [(vec (rseq verts))
     (reduce-kv (fn [acc k v] (assoc acc k (vec (rseq v)))) attribs attribs)])
#+END_SRC

** Protocol implementations

#+BEGIN_SRC clojure :noweb-ref impl
  (extend-type thi.ng.geom.types.IndexedMesh
#+END_SRC

*** PArea
#+BEGIN_SRC clojure :noweb-ref impl
  g/PArea
  (area
   [_] (gu/total-area-3d (mf/xf-face-verts _) (:faces _)))
#+END_SRC

*** PBounds

#+BEGIN_SRC clojure :noweb-ref impl
  g/PBounds
  (bounds [_] (tu/bounding-box (g/vertices _)))
  (width [_] (gu/axis-range 0 (g/vertices _)))
  (height [_] (gu/axis-range 1 (g/vertices _)))
  (depth [_] (gu/axis-range 2 (g/vertices _)))
#+END_SRC

*** PBoundingSphere

#+BEGIN_SRC clojure :noweb-ref impl
  g/PBoundingSphere
  (bounding-sphere
   [_] (tu/bounding-sphere (g/centroid _) (g/vertices _)))
#+END_SRC

*** PCenter

#+BEGIN_SRC clojure :noweb-ref impl
  g/PCenter
  (center
   ([_] (g/center _ (vec3)))
   ([_ o] (g/transform _ (g/translate M44 (g/- o (g/centroid _))))))
  (centroid
   [_] (gu/centroid (g/vertices _)))
#+END_SRC

*** PFlip

#+BEGIN_SRC clojure :noweb-ref impl
  g/PFlip
  (flip
   [_]
   (thi.ng.geom.types.IndexedMesh.
    (:vertices _)
    (mapv flip-face* (:faces _))
    (dissoc (:attribs _) :fnormals :vnormals)))
#+END_SRC

*** PGeomContainer

#+BEGIN_SRC clojure :noweb-ref impl
  g/PGeomContainer
  (into
   [_ faces] (tu/into-mesh _ add-face* faces))
  g/PClear
  (clear*
   [_] (indexed-mesh))
#+END_SRC

*** PVertexAccess, PEdgeAccess & PFaceAccess

#+BEGIN_SRC clojure :noweb-ref impl
  g/PVertexAccess
  (vertices
   [_] (-> _ :vertices :id->v))
  g/PEdgeAccess
  (edges
   [_] (err/unsupported!))
  g/PFaceAccess
  (faces
   ([_] (:faces _))
   ([_ opts]
    (if opts
      (map #(g/raw % _) (:faces _))
      (:faces _))))
  (add-face
   [_ f] (add-face* _ f))
  #_(vertex-faces
   [_ v] (vertex-faces* _ v))
  #_(remove-face
   [{:keys [vertices edges faces fnormals vnormals] :as _} f]
   (if (get faces f)
     (loop [verts vertices
            vnorms vnormals
            edges edges
            fedges (d/successive-nth 2 (conj f (first f)))]
       (if fedges
         (let [[a b] (first fedges)
               e #{a b}
               efaces (disj (get edges e) f)
               edges (if (seq efaces)
                       (assoc edges e efaces)
                       (dissoc edges e))
               ve (filter #(not= (:f %) f) (get verts a))]
           (if (seq ve)
             (recur (assoc verts a (into #{} ve)) vnorms edges (next fedges))
             (recur (dissoc verts a) (dissoc vnorms a) edges (next fedges))))
         (assoc _
           :vertices verts
           :vnormals vnorms
           :edges edges
           :faces (disj faces f)
           :fnormals (dissoc fnormals f))))
     _))
#+END_SRC

*** PNormalAccess

#+BEGIN_SRC clojure :noweb-ref impl
  g/PNormalAccess
  (face-normals
   [_ force?]
   (if-let [fns (-> _ :attribs :fnorm)]
     (:id->v fns)
     (if force? (-> (g/compute-face-normals _) :attribs :fnorm :id->v))))
  (face-normal
   [_ f]
   ((-> _ :attribs :fnorm :id->v)
    ((.-attribs ^thi.ng.geom.meshface.IndexedMeshFace f) :fnorm)))
  (vertex-normals
   [_ force?] (if force? (err/unsupported!)))
  (vertex-normal
   [_ v] (err/unsupported!))
  (compute-face-normals
   [_]
   (let [vertices (:vertices _)]
     (loop [fnorms (idx/monotonic-index), faces' (transient []), faces (:faces _)]
       (if faces
         (let [^thi.ng.geom.meshface.IndexedMeshFace f (first faces)
               fverts     (idx/attrib-values vertices (.-vertices f))
               [fnorms n] (idx/index fnorms (gu/ortho-normal fverts))
               fattr      (assoc (.-attribs f) :fnorm n)]
           (recur
            fnorms
            (conj! faces' (thi.ng.geom.meshface.IndexedMeshFace. (.-vertices f) fattr))
            (next faces)))
         (thi.ng.geom.types.IndexedMesh.
          vertices
          (persistent! faces')
          (assoc (:attribs _) :fnorm fnorms))))))
  (compute-vertex-normals
   [_] (err/unsupported!))
#+END_SRC

*** TODO PTessellate

Provide a map of attribute interpolators

#+BEGIN_SRC clojure :noweb-ref impl
  g/PTessellate
  (tessellate
   ([_]      (g/tessellate _ {:attribs (zipmap (keys (:attribs _)) (repeat g/mix))}))
   ([_ opts] (tu/map-mesh (or (:fn opts) (gu/tessellate-face gu/tessellate-with-first)) _)))
#+END_SRC

*** PTransform

#+BEGIN_SRC clojure :noweb-ref impl
  g/PScale
  (scale
   ([_ s]
    (tu/transform-mesh _ add-face* #(g/* % s)))
   ([_ sx sy sz]
    (tu/transform-mesh _ add-face* #(g/* % sx sy sz))))
  (scale-size
   [_ s]
   (let [c (g/centroid _)]
     (tu/transform-mesh _ add-face* #(g/madd (g/- % c) s c))))
  g/PTranslate
  (translate
   [_ t] (tu/transform-mesh _ add-face* #(g/+ % t)))
  g/PTransform
  (transform
   [_ tx]
   (tu/transform-mesh _ add-face* tx))
#+END_SRC

*** PVolume

#+BEGIN_SRC clojure :noweb-ref impl
  g/PVolume
  (volume
   [_] (gu/total-volume (mf/xf-face-verts _) (:faces _)))
#+END_SRC

*** End of implementation                                          :noexport:

#+BEGIN_SRC clojure :noweb-ref impl
  )
#+END_SRC

** Complete namespace definition

#+BEGIN_SRC clojure :tangle ../babel/src/thi/ng/geom/indexedmesh.cljc :noweb yes :mkdirp yes :padline no
  (ns thi.ng.geom.indexedmesh
    (:require
     [thi.ng.geom.core :as g]
     [thi.ng.geom.core.utils :as gu]
     [thi.ng.geom.core.vector :as v :refer [vec2 vec3]]
     [thi.ng.geom.core.matrix :refer [M44]]
     [thi.ng.geom.meshface :as mf]
     [thi.ng.geom.types]
     [thi.ng.geom.types.utils :as tu]
     [thi.ng.dstruct.core :as d]
     [thi.ng.dstruct.bidirindex :as idx]
     [thi.ng.math.core :as m :refer [*eps*]]
     [thi.ng.xerror.core :as err]
     [clojure.core.reducers :as r]
     [clojure.set :as set]))

  <<index>>

  <<ctors>>

  <<impl>>
#+END_SRC
