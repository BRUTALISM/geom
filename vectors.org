#+SEQ_TODO:       TODO(t) INPROGRESS(i) WAITING(w@) | DONE(d) CANCELED(c@)
#+TAGS:           Write(w) Update(u) Fix(f) Check(c) noexport(n)
#+EXPORT_EXCLUDE_TAGS: noexport

* Vector implementations
  For performance reasons, we decided to define custom datatypes
  for handling vectors & matrices. These types can be used much
  like standard Clojure vectors since they all implement the same
  protocols and interfaces, but also provide implementations for
  [[PVector]] and provide custom key lookups to provide swizzling features.

  Furthermore, since we aim for full ClojureScript compatibiliy,
  the following implementations are quite lengthy.
** Declarations
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  (declare vec2-op-1 vec2-op-2 vec2-op-2* vec2-op-3)
  (declare vec3-op-1 vec3-op-2 vec3-op-3)
#+END_SRC
** 2D
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx :mkdirp yes
  (deftype Vec2 [^double x ^double y _meta]
#+END_SRC
*** Clojure protocols
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  ,#+clj clojure.lang.IObj
  ,#+clj (meta [_] _meta)
  ,#+clj (withMeta [_ m*] (Vec2. x y m*))
  ,#+cljs IMeta
  ,#+cljs (-meta [_] _meta)
  ,#+cljs IWithMeta
  ,#+cljs (-with-meta [_ m*] (Vec2. x y m*))
  
  ,#+clj clojure.lang.ILookup
  ,#+clj (valAt [_ k] (swizzle2 _ k nil))
  ,#+clj (valAt [_ k nf] (swizzle2 _ k nf))
  ,#+cljs ILookup
  ,#+cljs (-lookup [_ k] (swizzle2 _ k nil))
  ,#+cljs (-lookup [_ k nf] (swizzle2 _ k nf))
  
  ,#+clj java.util.concurrent.Callable
  ,#+clj (call [_] (.invoke ^clojure.lang.IFn _))
  ,#+clj java.lang.Runnable
  ,#+clj (run [_] (.invoke ^clojure.lang.IFn _))
  
  ,#+clj clojure.lang.IFn
  ,#+clj (invoke [_ k] (swizzle2 _ k nil))
  ,#+clj (applyTo
         [_ args]
         (condp = (count args)
           1 (swizzle2 _ (first args) nil)
           2 (swizzle2 _ (first args) (second args))
           (arity-error (count args))))
  
  ,#+cljs IFn
  ,#+cljs (-invoke [_ k] (swizzle2 _ k nil))
  ,#+cljs (-applyTo
          [_ args]
          (condp = (count args)
            1 (swizzle2 _ (first args) nil)
            2 (swizzle2 _ (first args) (second args))
            (arity-error (count args))))
  
  ,#+clj clojure.lang.IMapEntry
  ,#+clj java.util.Map$Entry
  ,#+clj (key [_] x)
  ,#+clj (getKey [_] x)
  ,#+clj (val [_] y)
  ,#+clj (getValue [_] y)
  
  ,#+clj clojure.lang.Associative
  ,#+clj clojure.lang.IPersistentVector
  ,#+clj (count [_] 2)
  ,#+clj (length [_] 2)
  ,#+clj (containsKey [_ k] (not (nil? (#{0 1 :x :y} k))))
  ,#+clj (entryAt [_ k] (map-entry k (case (int k) 0 x, 1 y, (key-error k))))
  ,#+clj (assoc [_ k v]
          (case k
            :x (Vec2. v y _meta) :y (Vec2. x v _meta)
            0 (Vec2. v y _meta) 1 (Vec2. x v _meta)
            :xy (Vec2. (v 0) (v 1) _meta)
            :yx (Vec2. (v 1) (v 0) _meta)
            (key-error k)))
  ,#+clj (assocN [_ k v]
                (condp = k 0 (Vec2. v y _meta) 1 (Vec2. x v _meta) (key-error k)))
  
  ,#+cljs ICounted
  ,#+cljs (-count [_] 2)
  ,#+cljs IAssociative
  ,#+cljs IVector
  ,#+cljs (-contains-key? [_ k] (not (nil? (#{0 1 :x :y} k))))
  ,#+cljs (-assoc [_ k v]
                 (case k
                   :x (Vec2. v y _meta) :y (Vec2. x v _meta)
                   0 (Vec2. v y _meta) 1 (Vec2. x v _meta)
                   :xy (Vec2. (v 0) (v 1) _meta)
                   :yx (Vec2. (v 1) (v 0) _meta)
                   (key-error k)))
  ,#+cljs (-assoc-n [_ k v]
                   (case k
                     0 (Vec2. v y _meta) 1 (Vec2. x v _meta) (key-error k)))
  
  ,#+clj java.util.Collection
  ,#+clj (isEmpty [_] false)
  ,#+clj (iterator [_] (.iterator ^java.util.Collection (list x y)))
  ,#+clj (toArray [_] (double-array 2 [x y]))
  ,#+clj (size [_] 2)
  
  ,#+clj clojure.lang.IPersistentCollection
  ,#+clj clojure.lang.Indexed
  ,#+clj clojure.lang.Sequential
  ,#+clj clojure.lang.ISeq
  ,#+clj clojure.lang.Seqable
  ,#+clj (first [_] x)
  ,#+clj (next [_] (cons y nil))
  ,#+clj (more [_] (cons y nil))
  ,#+clj (cons [_ z] (with-meta (vec3 x y z) _meta))
  ,#+clj (peek [_] y)
  ,#+clj (pop [_] [x])
  ,#+clj (rseq [_] (Vec2. y x _meta))
  ,#+clj (seq [_] _)
  ,#+clj (nth [_ n] (case n 0 x, 1 y, (key-error n)))
  ,#+clj (nth [_ n nf] (case n 0 x, 1 y, nf))
  ,#+clj (equiv [_ o]
               (if (instance? Vec2 o)
                 (and (clojure.lang.Util/equiv x (.-x ^Vec2 o))
                      (clojure.lang.Util/equiv y (.-y ^Vec2 o)))
                 (and (instance? java.util.Collection o)
                      (= 2 (count o))
                      (clojure.lang.Util/equiv x (nth o 0))
                      (clojure.lang.Util/equiv y (nth o 1)))))
  ,#+clj (equals [_ o]
                (if (instance? Vec2 o)
                  (and (clojure.lang.Util/equals x (.-x ^Vec2 o))
                       (clojure.lang.Util/equals y (.-y ^Vec2 o)))
                  (and (instance? java.util.Collection o)
                       (= 2 (count o))
                       (clojure.lang.Util/equals x (nth o 0))
                       (clojure.lang.Util/equals y (nth o 1)))))
  ,#+cljs ISequential
  ,#+cljs ISeq
  ,#+cljs (-first [_] x)
  ,#+cljs (-rest [_] (cons y nil))
  ,#+cljs ISeqable
  ,#+cljs (-seq [_] _)
  ,#+cljs IReversible
  ,#+cljs (-rseq [_] (Vec2. y x _meta))
  ,#+cljs IIndexed
  ,#+cljs (-nth [_ n] (case n 0 x, 1 y, (key-error n)))
  ,#+cljs (-nth [_ n nf] (case n 0 x, 1 y, nf))
  
  ,#+clj Comparable
  ,#+clj (compareTo [_ o]
                   (if (instance? Vec2 o)
                     (let [c (compare x (.-x ^Vec2 o))]
                       (if (== 0 c) (compare y (.-y ^Vec2 o)) c))
                     (let [c (count o)]
                       (if (== 2 c) (compare o _) (- 2 c)))))
  ,#+clj (hashCode [_]
                  (hash-combine (clojure.lang.Util/hash x) (clojure.lang.Util/hash y)))
  
  ,#+cljs IComparable
  ,#+cljs (-compare [_ o]
                   (let [c (compare x (.-x ^Vec2 o))]
                     (if (== 0 c) (compare y (.-y ^Vec2 o)) c)))
  
  ,#+cljs IHash
  ,#+cljs (-hash [_] (hash-combine (hash x) (hash y)))
  
  Object
  (toString [_] (str "[" x " " y "]"))
#+END_SRC
*** PTransformable
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PTransformable
  (scale [_ a]
    (if (instance? Vec2 a)
      (Vec2. (c* x (.-x ^Vec2 a)) (c* y (.-y ^Vec2 a)) _meta)
      (if (number? a)
        (Vec2. (c* x a) (c* y a) _meta)
        (Vec2. (c* x (nth a 0 1)) (c* y (nth a 1 1)) _meta))))
  (scale [_ a b]
    (if (number? a)
      (Vec2. (c* x a) (c* y b) _meta)
      (let [[ax ay] a [bx by] b]
        (Vec2. (c* (c* x ax) bx) (c* (c* y ay) by) _meta))))
  (translate [_ [tx ty]]
    (+ _ tx ty))
  (rotate [_ theta]
    (let [s (Math/sin theta) c (Math/cos theta)]
      (Vec2. (c- (c* x c) (c* y s)) (c+ (c* x s) (c* y c)) _meta)))
  (rotate-z [_ theta]
    (rotate _ theta))
  (transform [_ m])
#+END_SRC
*** PMathOps
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PMathOps
  (abs [_] (Vec2. (m/abs x) (m/abs y) _meta))
  (+ [_] _)
  (+ [_ a] (vec2-op-1 c+ _ a 0.0 _meta))
  (+ [_ a b] (vec2-op-2 c+ _ a b 0.0 0.0 _meta))
  (+ [_ a b c] (vec2-op-3 c+ _ a b c 0.0 _meta))
  
  (- [_] (Vec2. (c- x) (c- y) _meta))
  (- [_ a] (vec2-op-1 c- _ a 0.0 _meta))
  (- [_ a b] (vec2-op-2 c- _ a b 0.0 0.0 _meta))
  (- [_ a b c] (vec2-op-3 c- _ a b c 0.0 _meta))
  
  (* [_] _)
  (* [_ a] (vec2-op-1 c* _ a 1.0 _meta))
  (* [_ a b] (vec2-op-2 c* _ a b 1.0 1.0 _meta))
  (* [_ a b c] (vec2-op-3 c* _ a b c 1.0 _meta))
  
  (div [_] (Vec2. (/ x) (/ y) _meta))
  (div [_ a] (vec2-op-1 / _ a 0.0 _meta))
  (div [_ a b] (vec2-op-2 / _ a b 0.0 0.0 _meta))
  (div [_ a b c] (vec2-op-3 / _ a b c 0.0 _meta))
  
  (madd [_ a b] (vec2-op-2* c* c+ _ a b 1.0 0.0 _meta))
#+END_SRC
*** PDotProduct
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PDotProduct
  (dot [_ [ax ay]] (c+ (c* x ax) (c* y ay)))
#+END_SRC
*** PCrossProduct
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx  
  PCrossProduct
  (cross [_ [ax ay]] (c- (c* x ay) (c* y ax)))
#+END_SRC
*** PLimit
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx  
  PLimit
  (limit [_ len]
         (if (> (mag-squared _) (c* len len))
           (normalize _ len) _))
#+END_SRC
*** PMinMax
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PMinMax
  (min [_ [ax ay]]
       (Vec2. (cmin x ax) (cmin y ay) _meta))
  (min [_ [ax ay] [bx by]]
       (Vec2. (cmin (cmin x ax) bx) (cmin (cmin y ay) by) _meta))
  (max [_ [ax ay]]
       (Vec2. (cmax x ax) (cmax y ay) _meta))
  (max [_ [ax ay] [bx by]]
       (Vec2. (cmax (cmax x ax) bx) (cmax (cmax y ay) by) _meta))
#+END_SRC
*** PMagnitude
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PMagnitude
  (mag [_] (Math/sqrt (c+ (c* x x) (c* y y))))
  (mag-squared [_] (c+ (c* x x) (c* y y)))
#+END_SRC
*** PNormalize
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PNormalize
  (normalize [_]
    (let [l (mag _)]
      (if (pos? l) (Vec2. (/ x l) (/ y l) _meta) _)))
  (normalize [_ len]
    (let [l (mag _)]
      (if (pos? l)
        (let [l (/ len l)] (Vec2. (c* x l) (c* y l) _meta)) _)))
  (normalized? [_]
    (m/delta= 1.0 (mag-squared _)))
#+END_SRC
*** PDistance
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PDistance
  (dist
   [_ a]
   (let [[dx dy] (if (instance? Vec2 a)
                   [(c- x (.-x ^Vec2 a)) (c- y (.-y ^Vec2 a))]
                   [(c- x (nth a 0)) (c- y (nth a 1))])]
     (Math/sqrt (c+ (c* dx dx) (c* dy dy)))))
  (dist-squared
   [_ a]
   (let [[dx dy] (if (instance? Vec2 a)
                   [(c- x (.-x ^Vec2 a)) (c- y (.-y ^Vec2 a))]
                   [(c- x (nth a 0)) (c- y (nth a 1))])]
     (c+ (c* dx dx) (c* dy dy))))
#+END_SRC
*** PReflect
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PReflect
  (reflect [_ [rx ry :as r]]
    (let [d (c* (dot _ r) 2.0)]
      (Vec2. (c- (c* rx d) x) (c- (c* ry d) y) _meta)))
#+END_SRC
*** PInvertible
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
    PInvertible
    (invert [_] (Vec2. (c- x) (c- y) _meta))
#+END_SRC
*** PInterpolateable
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PInterpolateable
  (mix [_ [ax ay]]
    (Vec2. (c+ x (c* (c- ax x) 0.5)) (c+ y (c* (c- ay y) 0.5)) _meta))
  (mix [_ [ax ay] t]
    (Vec2. (c+ x (c* (c- ax x) t)) (c+ y (c* (c- ay y) t)) _meta))
#+END_SRC
*** PHeading
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PHeading
  (heading-xy [_]
    (let [t (Math/atan2 y x)]
      (if (neg? t) (c+ TWO_PI t) t)))
  (angle-between [_ a]
    (let [t (c- (heading-xy a) (heading-xy _))]
      (if (neg? t) (c+ TWO_PI t) t)))
  (slope-xy [_] (/ y x))
  (normal [_] (Vec2. (c- y) x _meta))
#+END_SRC
*** PPolar
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
    PPolar
    (as-polar [_]
      (Vec2. (mag _) (heading-xy _) _meta))
    (as-cartesian [_]
      (Vec2. (c* x (Math/cos y)) (c* x (Math/sin y)) _meta))
#+END_SRC
*** End of implementations
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  )
#+END_SRC
*** Override print methods
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  #+clj (require 'clojure.pprint)
  #+clj (prefer-method clojure.pprint/simple-dispatch clojure.lang.IPersistentVector clojure.lang.ISeq)
  #+clj (defmethod print-method Vec2 [o ^java.io.Writer w] (.write w (.toString o)))
#+END_SRC
** 3D
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  (deftype Vec3 [^double x ^double y ^double z _meta]
#+END_SRC
*** Clojure protocols
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  ,#+clj clojure.lang.IObj
  ,#+clj (meta [_] _meta)
  ,#+clj (withMeta [_ m*] (Vec3. x y z m*))
  ,#+cljs IMeta
  ,#+cljs (-meta [_] _meta)
  ,#+cljs IWithMeta
  ,#+cljs (-with-meta [_ m*] (Vec3. x y z m*))
  
  ,#+clj clojure.lang.ILookup
  ,#+clj (valAt [_ k] (swizzle3 _ k nil))
  ,#+clj (valAt [_ k nf] (swizzle3 _ k nf))
  ,#+cljs ILookup
  ,#+cljs (-lookup [_ k] (swizzle3 _ k nil))
  ,#+cljs (-lookup [_ k nf] (swizzle3 _ k nf))
  
  ,#+clj java.util.concurrent.Callable
  ,#+clj (call [_] (.invoke ^clojure.lang.IFn _))
  ,#+clj java.lang.Runnable
  ,#+clj (run [_] (.invoke ^clojure.lang.IFn _))
  
  ,#+clj clojure.lang.IFn
  ,#+clj (invoke [_ k] (swizzle3 _ k nil))
  ,#+clj (applyTo
         [_ args]
         (condp = (count args)
           1 (swizzle3 _ (first args) nil)
           2 (swizzle3 _ (first args) (second args))
           (arity-error (count args))))
  ,#+cljs IFn
  ,#+cljs (-invoke [_ k] (swizzle3 _ k nil))
  ,#+cljs (-applyTo
          [_ args]
          (condp = (count args)
            1 (swizzle3 _ (first args) nil)
            2 (swizzle3 _ (first args) (second args))
            (arity-error (count args))))
  
  ,#+clj clojure.lang.Associative
  ,#+clj clojure.lang.IPersistentVector
  ,#+clj (count [_] 3)
  ,#+clj (length [_] 3)
  ,#+clj (containsKey [_ k] (not (nil? (#{0 1 2 :x :y :z} k))))
  ,#+clj (entryAt [_ k] (map-entry k (case (int k) 0 x, 1 y, 2 z, (key-error k))))
  ,#+clj (assoc [_ k v]
          (case k
            :x (Vec3. v y z _meta)
            :y (Vec3. x v z _meta)
            :z (Vec3. x y v _meta)
            0 (Vec3. v y z _meta)
            1 (Vec3. x v z _meta)
            2 (Vec3. x y v _meta)
            :xy (Vec3. (v 0) (v 1) z _meta) ;; (assoc [x y z] :xy [a b]) => [a b z]
            :yx (Vec3. (v 1) (v 0) z _meta) ;; (assoc [x y z] :yx [a b]) => [b a z]
            :xz (Vec3. (v 0) y (v 1) _meta) ;; (assoc [x y z] :xz [a b]) => [a y b]
            :zx (Vec3. (v 1) y (v 0) _meta) ;; (assoc [x y z] :zx [a b]) => [b y a]
            :yz (Vec3. x (v 0) (v 1) _meta) ;; (assoc [x y z] :yz [a b]) => [x a b]
            :zy (Vec3. x (v 1) (v 0) _meta) ;; (assoc [x y z] :zy [a b]) => [x b a]
            (key-error k)))
  ,#+clj (assocN
         [_ k v]
         (case k
           0 (Vec3. v y z _meta)
           1 (Vec3. x v z _meta)
           2 (Vec3. x y v _meta)
           (key-error k)))
  
  ,#+cljs ICounted
  ,#+cljs (-count [_] 2)
  ,#+cljs IAssociative
  ,#+cljs IVector
  ,#+cljs (-contains-key? [_ k]
                         (not (nil? (#{0 1 2 :x :y :z} k))))
  ,#+cljs (-assoc
          [_ k v]
          (case k
            :x (Vec3. v y z _meta)
            :y (Vec3. x v z _meta)
            :z (Vec3. x y v _meta)
            0 (Vec3. v y z _meta)
            1 (Vec3. x v z _meta)
            2 (Vec3. x y v _meta)
            :xy (Vec3. (v 0) (v 1) z _meta) ;; (assoc [x y z] :xy [a b]) => [a b z]
            :yx (Vec3. (v 1) (v 0) z _meta) ;; (assoc [x y z] :yx [a b]) => [b a z]
            :xz (Vec3. (v 0) y (v 1) _meta) ;; (assoc [x y z] :xz [a b]) => [a y b]
            :zx (Vec3. (v 1) y (v 0) _meta) ;; (assoc [x y z] :zx [a b]) => [b y a]
            :yz (Vec3. x (v 0) (v 1) _meta) ;; (assoc [x y z] :yz [a b]) => [x a b]
            :zy (Vec3. x (v 1) (v 0) _meta) ;; (assoc [x y z] :zy [a b]) => [x b a]
            (key-error k)))
  ,#+cljs (-assoc-n
          [_ k v]
          (case k
            0 (Vec3. v y z _meta)
            1 (Vec3. x v z _meta)
            2 (Vec3. x y v _meta)
            (key-error k)))
  
  ,#+clj java.util.Collection
  ,#+clj (isEmpty [_] false)
  ,#+clj (iterator [_] (.iterator ^java.util.Collection (list x y z)))
  ,#+clj (toArray [_] (double-array 3 [x y z]))
  ,#+clj (size [_] 3)
  
  ,#+clj clojure.lang.IPersistentCollection
  ,#+clj clojure.lang.Indexed
  ,#+clj clojure.lang.Sequential
  ,#+clj clojure.lang.ISeq
  ,#+clj clojure.lang.Seqable
  ,#+clj (first [_] x)
  ,#+clj (next [_] (cons y (cons z nil)))
  ,#+clj (more [_] (cons y (cons z nil)))
  ,#+clj (cons [_ w] (with-meta [x y z w] _meta))
  ,#+clj (peek [_] z)
  ,#+clj (pop [_] (Vec2. x y _meta))
  ,#+clj (rseq [_] (Vec3. z y x _meta))
  ,#+clj (seq [_] _)
  ,#+clj (nth [_ n] (condp = n 0 x 1 y 2 z (key-error n)))
  ,#+clj (nth [_ n nf] (condp = n 0 x 1 y 2 z nf))
  ,#+clj (equiv
         [_ o]
         (if (instance? Vec3 o)
           (and (clojure.lang.Util/equiv x (.-x ^Vec3 o))
                (clojure.lang.Util/equiv y (.-y ^Vec3 o))
                (clojure.lang.Util/equiv z (.-z ^Vec3 o)))
           (and (instance? java.util.Collection o)
                (= 3 (count o))
                (clojure.lang.Util/equiv x (nth o 0))
                (clojure.lang.Util/equiv y (nth o 1))
                (clojure.lang.Util/equiv z (nth o 2)))))
  ,#+clj (equals
         [_ o]
         (if (instance? Vec3 o)
           (and (clojure.lang.Util/equals x (.-x ^Vec3 o))
                (clojure.lang.Util/equals y (.-y ^Vec3 o))
                (clojure.lang.Util/equals z (.-z ^Vec3 o)))
           (and (instance? java.util.Collection o)
                (= 3 (count o))
                (clojure.lang.Util/equals x (nth o 0))
                (clojure.lang.Util/equals y (nth o 1))
                (clojure.lang.Util/equals z (nth o 2)))))
  ,#+cljs ISequential
  ,#+cljs ISeq
  ,#+cljs (-first [_] x)
  ,#+cljs (-rest [_] (cons y (cons z nil)))
  ,#+cljs ISeqable
  ,#+cljs (-seq [_] _)
  ,#+cljs IReversible
  ,#+cljs (-rseq [_] (Vec3. z y x _meta))
  ,#+cljs IIndexed
  ,#+cljs (-nth [_ n] (condp = n 0 x 1 y 2 z (key-error n)))
  ,#+cljs (-nth [_ n nf] (condp = n 0 x 1 y 2 z nf))
  
  ,#+clj Comparable
  ,#+clj (compareTo
         [_ o]
         (if (instance? Vec3 o)
           (let [c (compare x (.-x ^Vec3 o))]
             (if (== 0 c)
               (let [c (compare y (.-y ^Vec3 o))]
                 (if (== 0 c)
                   (compare z (.-z ^Vec3 o))
                   c))
               c))
           (let [c (count o)]
             (if (== 3 c) (compare o _) (- 3 c)))))
  ,#+clj (hashCode
         [_]
         (-> x
             (clojure.lang.Util/hash)
             (hash-combine (clojure.lang.Util/hash y))
             (hash-combine (clojure.lang.Util/hash z))))
  ,#+cljs IComparable
  ,#+cljs (-compare
          [_ o]
          (let [c (compare x (.-x ^Vec3 o))]
            (if (== 0 c)
              (let [c (compare y (.-y ^Vec3 o))]
                (if (== 0 c) (compare z (.-z ^Vec3 o)) c))
              c)))
  
  ,#+clj clojure.lang.IHashEq
  ,#+clj (hasheq [_]
                (-> x
                    (clojure.lang.Util/hasheq)
                    (hash-combine (clojure.lang.Util/hasheq y))
                    (hash-combine (clojure.lang.Util/hasheq z))))
  ,#+cljs IHash
  ,#+cljs (-hash [_] (-> x (hash) (hash-combine (hash y)) (hash-combine (hash z))))
  
  Object
  (toString [_] (str "[" x " " y " " z "]"))
#+END_SRC
*** PTransformable
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PTransformable
  (scale [_ a]
    (if (instance? Vec3 a)
      (Vec3. (c* x (.-x ^Vec3 a)) (c* y (.-y ^Vec3 a)) (c* z (.-z ^Vec3 a)) _meta)
      (if (number? a)
        (Vec3. (c* x a) (c* y a) (c* z a) _meta)
        (Vec3. (c* x (nth a 0 1)) (c* y (nth a 1 1)) (c* z (nth a 2 1)) _meta))))
  (scale [_ a b]
    (if (number? a)
      (Vec3. (c* x a) (c* y b) z _meta)
      (let [[ax ay az] a [bx by bz] b]
        (Vec3. (c* (c* x ax) bx) (c* (c* y ay) by) (c* (c* z az) bz) _meta))))
  (scale [_ a b c]
    (if (number? a)
      (Vec3. (c* x a) (c* y b) (c* z c) _meta)
      (let [[ax ay az] a [bx by bz] b [cx cy cz] c]
        (Vec3. (c* (c* (c* x ax) bx) cx) (c* (c* (c* y ay) by) cy) (c* (c* (c* z az) bz) cz) _meta))))
  (translate [_ t]
    (+ _ t))
  (rotate-x [_ theta]
    (let [s (Math/sin theta) c (Math/cos theta)]
      (Vec3. x (c- (c- (c* z c) (c* y s))) (c- (c+ (c* z s) (c* y c))) _meta)))
  (rotate-y [_ theta]
    (let [s (Math/sin theta) c (Math/cos theta)]
      (Vec3. (c- (c- (c* x c) (c* z s))) y (c+ (c* x s) (c* z c)) _meta)))
  (rotate-z [_ theta]
    (let [s (Math/sin theta) c (Math/cos theta)]
      (Vec3. (c- (c* x c) (c* y s)) (c+ (c* x s) (c* y c)) z _meta)))
  (rotate [_ theta]
    (rotate-z _ theta))
  (transform [_ m] (transform-vector m _))
#+END_SRC
*** PMathOps
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PMathOps
  (+ [_] _)
  (+ [_ a] (vec3-op-1 c+ _ a 0.0 _meta))
  (+ [_ a b] (vec3-op-2 c+ c+ _ a b 0.0 0.0 _meta))
  (+ [_ a b c] (vec3-op-3 c+ _ a b c 0.0 _meta))
  
  (- [_] (Vec3. (c- x) (c- y) (c- z) _meta))
  (- [_ a] (vec3-op-1 c- _ a 0.0 _meta))
  (- [_ a b] (vec3-op-2 c- c- _ a b 0.0 0.0 _meta))
  (- [_ a b c] (vec3-op-3 c- _ a b c 0.0 _meta))
  
  (* [_] _)
  (* [_ a] (vec3-op-1 c* _ a 1.0 _meta))
  (* [_ a b] (vec3-op-2 c* c* _ a b 1.0 1.0 _meta))
  (* [_ a b c] (vec3-op-3 c* _ a b c 1.0 _meta))
  
  (div [_] (Vec3. (/ x) (/ y) (/ z) _meta))
  (div [_ a] (vec3-op-1 / _ a 0.0 _meta))
  (div [_ a b] (vec3-op-2 / / _ a b 0.0 0.0 _meta))
  (div [_ a b c] (vec3-op-3 / _ a b c 0.0 _meta))
  
  (madd [_ a b] (vec3-op-2 c* c+ _ a b 1.0 0.0 _meta))
  
  PDotProduct
  (dot [_ [ax ay az]] (c+ (c+ (c* x ax) (c* y ay)) (c* z az)))
  
  PCrossProduct
  (cross [_ [ax ay az]]
         (Vec3. (c- (c* y az) (c* ay z)) (c- (c* z ax) (c* az x)) (c- (c* x ay) (c* ax y)) _meta))
  
  PLimit
  (limit [_ len]
         (if (> (mag-squared _) (c* len len)) (normalize _ len) _))
  
  PMinMax
  (min [_ [ax ay az]]
       (Vec3. (cmin x ax) (cmin y ay) (cmin z az) _meta))
  (min [_ [ax ay az] [bx by bz]]
       (Vec3. (cmin (cmin x ax) bx) (cmin (cmin y ay) by) (cmin (cmin z az) bz) _meta))
  (max [_ [ax ay az]]
       (Vec3. (cmax x ax) (cmax y ay) (cmax z az) _meta))
  (max [_ [ax ay az] [bx by bz]]
       (Vec3. (cmax (cmax x ax) bx) (cmax (cmax y ay) by) (cmax (cmax z az) bz) _meta))
#+END_SRC
*** PMagnitude
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PMagnitude
  (mag [_] (Math/sqrt (c+ (c+ (c* x x) (c* y y)) (c* z z))))
  (mag-squared [_] (c+ (c+ (c* x x) (c* y y)) (c* z z)))
#+END_SRC
*** PNormalize
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PNormalize
  (normalize [_]
    (let [l (mag _)]
      (if (pos? l) (Vec3. (/ x l) (/ y l) (/ z l) _meta) _)))
  (normalize [_ len]
    (let [l (mag _)]
      (if (pos? l)
        (let [l (/ len l)] (Vec3. (c* x l) (c* y l) (c* z l) _meta)) _)))
  (normalized? [_]
    (m/delta= 1.0 (mag-squared _)))
#+END_SRC
*** PDistance
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PDistance
  (dist
   [_ a]
   (let [[dx dy dz] (if (instance? Vec3 a)
                      [(c- x (.-x ^Vec3 a)) (c- y (.-y ^Vec3 a)) (c- z (.-z ^Vec3 a))]
                      [(c- x (nth a 0)) (c- y (nth a 1)) (c- z (nth a 2 0.0))])]
     (Math/sqrt (c+ (c+ (c* dx dx) (c* dy dy)) (c* dz dz)))))
  (dist-squared
   [_ a]
   (let [[dx dy dz] (if (instance? Vec3 a)
                      [(c- x (.-x ^Vec3 a)) (c- y (.-y ^Vec3 a)) (c- z (.-z ^Vec3 a))]
                      [(c- x (nth a 0)) (c- y (nth a 1)) (c- z (nth a 2 0.0))])]
     (c+ (c+ (c* dx dx) (c* dy dy)) (c* dz dz))))
#+END_SRC
*** PReflect
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PReflect
  (reflect [_ [rx ry rz :as r]]
    (let [d (c* (dot _ r) 2.0)]
      (Vec3. (c- (c* rx d) x) (c- (c* ry d) y) (c- (c* rz d) z) _meta)))
#+END_SRC
*** PInvertible
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PInvertible
  (invert [_] (Vec3. (c- x) (c- y) (c- z) _meta))
#+END_SRC
*** PInterpolateable
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  PInterpolateable
  (mix [_ [ax ay az]]
    (Vec3. (c+ x (c* (c- ax x) 0.5)) (c+ y (c* (c- ay y) 0.5)) (c+ z (c* (c- az z) 0.5)) _meta))
  (mix [_ [ax ay az] t]
    (Vec3. (c+ x (c* (c- ax x) t)) (c+ y (c* (c- ay y) t)) (c+ z (c* (c- az z) t)) _meta))
#+END_SRC
*** PPolar
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  ;; http://mathworld.wolfram.com/SphericalCoordinates.html
  PPolar
  (as-polar [_]
    (let [r (Math/sqrt (c+ (c+ (c* x x) (c* y y)) (c* z z)))
          theta (Math/atan2 y x)
          phi (Math/acos (/ z r))]
      (Vec3. r theta phi _meta)))
  (as-cartesian [_]
    (let [rsphi (c* x (Math/sin z))]
      (Vec3. (c* (Math/cos y) rsphi) (c* (Math/sin y) rsphi) (c* x (Math/cos z)) _meta)))
#+END_SRC
*** End of implementations
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
    )
#+END_SRC
*** Override print methods
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  #+clj (defmethod print-method Vec3 [o ^java.io.Writer w] (.write w (.toString o)))
#+END_SRC
** Vector operations
*** 2D
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  (defn vec2-op-1
    [op ^Vec2 _ a d m]
    (if (instance? Vec2 a)
      (Vec2.
       (op (.-x _) (.-x ^Vec2 a))
       (op (.-y _) (.-y ^Vec2 a)) m)
      (if (number? a)
        (Vec2. (op (.-x _) a) (op (.-y _) a) m)
        (Vec2.
         (op (.-x _) (nth a 0 d))
         (op (.-y _) (nth a 1 d)) m))))
  
  (defn vec2-op-2
    [op ^Vec2 _ a b d1 d2 m]
    (if (instance? Vec2 a)
      (if (instance? Vec2 b)
        (Vec2. ;; v v
         (op (op (.-x _) (.-x ^Vec2 a)) (.-x ^Vec2 b))
         (op (op (.-y _) (.-y ^Vec2 a)) (.-y ^Vec2 b)) m)
        (if (number? b)
          (Vec2. ;; v n
           (op (op (.-x _) (.-x ^Vec2 a)) b)
           (op (op (.-y _) (.-y ^Vec2 a)) b) m)
          (Vec2. ;; v ?
           (op (op (.-x _) (.-x ^Vec2 a)) (nth b 0 d2))
           (op (op (.-y _) (.-y ^Vec2 a)) (nth b 1 d2)) m)))
      (let [an? (number? a) bn? (number? b)]
        (if (and an? bn?)
          (Vec2. ;; n n
           (op (.-x _) a)
           (op (.-y _) b) m)
          (let [[ax ay] (if an? [a a] a)
                [bx by] (if bn? [b b] b)]
            (Vec2. ;; ? ?
             (op (op (.-x _) (or ax d1)) (or bx d2))
             (op (op (.-y _) (or ay d1)) (or by d2)) m))))))
  
  (defn vec2-op-2*
    "Similar to vec2-op-2 but takes 2 fn args and handles case with
    a & b being both numbers differently."
    [op op2 ^Vec2 _ a b d1 d2 m]
    (if (instance? Vec2 a)
      (if (instance? Vec2 b)
        (Vec2. ;; v v
         (op2 (op (.-x _) (.-x ^Vec2 a)) (.-x ^Vec2 b))
         (op2 (op (.-y _) (.-y ^Vec2 a)) (.-y ^Vec2 b)) m)
        (if (number? b)
          (Vec2. ;; v n
           (op2 (op (.-x _) (.-x ^Vec2 a)) b)
           (op2 (op (.-y _) (.-y ^Vec2 a)) b) m)
          (Vec2. ;; v ?
           (op2 (op (.-x _) (.-x ^Vec2 a)) (nth b 0 d2))
           (op2 (op (.-y _) (.-y ^Vec2 a)) (nth b 1 d2)) m)))
      (let [an? (number? a) bn? (number? b)]
        (if (and an? bn?)
          (Vec2. ;; n n
           (op2 (op (.-x _) a) b)    ;; difference to vec2-op-2
           (op2 (op (.-y _) a) b) m)
          (let [[ax ay] (if an? [a a] a)
                [bx by] (if bn? [b b] b)]
            (Vec2. ;; ? ?
             (op2 (op (.-x _) (or ax d1)) (or bx d2))
             (op2 (op (.-y _) (or ay d1)) (or by d2)) m))))))
  
  (defn vec2-op-3
    [op ^Vec2 _ a b c d m]
    (if (instance? Vec2 a)
      (if (instance? Vec2 b)
        (if (instance? Vec2 c)
          (Vec2. ;; v v v
           (op (op (op (.-x _) (.-x ^Vec2 a)) (.-x ^Vec2 b)) (.-x ^Vec2 c))
           (op (op (op (.-y _) (.-y ^Vec2 a)) (.-y ^Vec2 b)) (.-y ^Vec2 c)) m)
          (if (number? c)
            (Vec2. ;; v v n
             (op (op (op (.-x _) (.-x ^Vec2 a)) (.-x ^Vec2 b)) c)
             (op (op (op (.-y _) (.-y ^Vec2 a)) (.-y ^Vec2 b)) c) m)
            (Vec2. ;; v v ?
             (op (op (op (.-x _) (.-x ^Vec2 a)) (.-x ^Vec2 b)) (nth c 0 d))
             (op (op (op (.-y _) (.-y ^Vec2 a)) (.-y ^Vec2 b)) (nth c 1 d)) m)))
        (let [[bx by] (if (number? b) [b b] b)
              [cx cy] (if (number? c) [c c] c)]
          (Vec2. ;; v ? ?
           (op (op (op (.-x _) (.-x ^Vec2 a)) (or bx d)) (or cx d))
           (op (op (op (.-y _) (.-y ^Vec2 a)) (or by d)) (or cy d)) m)))
      (let [[ax ay] (if (number? a) [a a] a)
            [bx by] (if (number? b) [b b] b)
            [cx cy] (if (number? c) [c c] c)]
        (Vec2. ;; ? ? ?
         (op (op (op (.-x _) (or ax d)) (or bx d)) (or cx d))
         (op (op (op (.-y _) (or ay d)) (or by d)) (or cy d)) m))))
#+END_SRC
*** 3D
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  (defn vec3-op-1
    [op ^Vec3 _ a d m]
    (if (instance? Vec3 a)
      (Vec3.
       (op (.-x _) (.-x ^Vec3 a))
       (op (.-y _) (.-y ^Vec3 a))
       (op (.-z _) (.-z ^Vec3 a)) m)
      (if (number? a)
        (Vec3. (op (.-x _) a) (op (.-y _) a) (op (.-z _) a) m)
        (Vec3.
         (op (.-x _) (nth a 0 d))
         (op (.-y _) (nth a 1 d))
         (op (.-z _) (nth a 2 d)) m))))
  
  (defn vec3-op-2
    [op op2 ^Vec3 _ a b d1 d2 m]
    (if (instance? Vec3 a)
      (if (instance? Vec3 b)
        (Vec3. ;; v v
         (op2 (op (.-x _) (.-x ^Vec3 a)) (.-x ^Vec3 b))
         (op2 (op (.-y _) (.-y ^Vec3 a)) (.-y ^Vec3 b))
         (op2 (op (.-z _) (.-z ^Vec3 a)) (.-z ^Vec3 b)) m)
        (if (number? b)
          (Vec3. ;; v n
           (op2 (op (.-x _) (.-x ^Vec3 a)) b)
           (op2 (op (.-y _) (.-y ^Vec3 a)) b)
           (op2 (op (.-z _) (.-z ^Vec3 a)) b) m)
          (Vec3. ;; v ?
           (op2 (op (.-x _) (.-x ^Vec3 a)) (nth b 0 d2))
           (op2 (op (.-y _) (.-y ^Vec3 a)) (nth b 1 d2))
           (op2 (op (.-z _) (.-z ^Vec3 a)) (nth b 2 d2)) m)))
      (let [[ax ay az] (if (number? a) [a a a] a)
            [bx by bz] (if (number? b) [b b b] b)]
        (Vec3. ;; ? ? ?
         (op2 (op (.-x _) (or ax d1)) (or bx d2))
         (op2 (op (.-y _) (or ay d1)) (or by d2))
         (op2 (op (.-z _) (or az d1)) (or bz d2)) m))))
  
  (defn vec3-op-3
    [op ^Vec3 _ a b c d m]
    (if (instance? Vec3 a)
      (if (instance? Vec3 b)
        (if (instance? Vec3 c)
          (Vec3. ;; v v v
           (op (op (op (.-x _) (.-x ^Vec3 a)) (.-x ^Vec3 b)) (.-x ^Vec3 c))
           (op (op (op (.-y _) (.-y ^Vec3 a)) (.-y ^Vec3 b)) (.-y ^Vec3 c))
           (op (op (op (.-z _) (.-z ^Vec3 a)) (.-z ^Vec3 b)) (.-z ^Vec3 c)) m)
          (if (number? c)
            (Vec3. ;; v v n
             (op (op (op (.-x _) (.-x ^Vec3 a)) (.-x ^Vec3 b)) c)
             (op (op (op (.-y _) (.-y ^Vec3 a)) (.-y ^Vec3 b)) c)
             (op (op (op (.-z _) (.-z ^Vec3 a)) (.-z ^Vec3 b)) c) m)
            (Vec3. ;; v v ?
             (op (op (op (.-x _) (.-x ^Vec3 a)) (.-x ^Vec3 b)) (nth c 0 d))
             (op (op (op (.-y _) (.-y ^Vec3 a)) (.-y ^Vec3 b)) (nth c 1 d))
             (op (op (op (.-z _) (.-z ^Vec3 a)) (.-z ^Vec3 b)) (nth c 2 d)) m)))
        (let [[bx by bz] (if (number? b) [b b b] b)
              [cx cy cz] (if (number? c) [c c c] c)]
          (Vec3. ;; v ? ?
           (op (op (op (.-x _) (.-x ^Vec3 a)) (or bx d)) (or cx d))
           (op (op (op (.-y _) (.-y ^Vec3 a)) (or by d)) (or cy d))
           (op (op (op (.-z _) (.-z ^Vec3 a)) (or bz d)) (or cz d)) m)))
      (let [an? (number? a) bn? (number? b) cn? (number? c)]
        (if (and an? bn? cn?)
          (Vec3. ;; n n n
           (op (.-x _) a)
           (op (.-y _) b)
           (op (.-z _) c) m)
          (let [[ax ay az] (if (number? a) [a a a] a)
                [bx by bz] (if (number? b) [b b b] b)
                [cx cy cz] (if (number? c) [c c c] c)]
            (Vec3. ;; ? ? ?
             (op (op (op (.-x _) (or ax d)) (or bx d)) (or cx d))
             (op (op (op (.-y _) (or ay d)) (or by d)) (or cy d))
             (op (op (op (.-z _) (or az d)) (or bz d)) (or cz d)) m))))))
#+END_SRC
** Constants
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  (def ^:const VEC2 (Vec2. 0.0 0.0 nil))
  (def ^:const VEC3 (Vec3. 0.0 0.0 0.0 nil))

  (def ^:const V2_X (Vec2. 1.0 0.0 nil))
  (def ^:const V2_Y (Vec2. 0.0 1.0 nil))

  (def ^:const V3_X (Vec3. 1.0 0.0 0.0 nil))
  (def ^:const V3_Y (Vec3. 0.0 1.0 0.0 nil))
  (def ^:const V3_Z (Vec3. 0.0 0.0 1.0 nil))
#+END_SRC
** Constructors
      To simplify vector type construction, factory functions
      are provided which coerce arguments into vectors with these
      rules:

      | Input                 | Behavior                                           | Examples call        | Example result  |
      |-----------------------+----------------------------------------------------+----------------------+-----------------|
      | Clojure vector or seq | select first =n= elements or fill missing with 0.0 | =(vec2 [1 2 3 4])=   | =[1.0 2.0]=     |
      |                       |                                                    | =(vec3 '(1 2)=       | =[1.0 2.0 0.0]= |
      | Map                   | select values of =:x :y :z= or fill with 0.0       | =(vec2 {:x 1 :y 2})= | =[1.0 2.0]=     |
      |                       |                                                    | =(vec3 {:x 1 :y 2})= | =[1.0 2.0 0.0]= |
      | Single number         | set all vector components                          | =(vec3 1)=           | =[1.0 1.0 1.0]= |

#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  (defn vec2
    ([] (Vec2. 0.0 0.0 nil))
    ([x] (cond
          (instance? Vec2 x) x
          (sequential? x) (Vec2. (nth x 0 0.0) (nth x 1 0.0) (meta x))
          (number? x) (Vec2. x x nil)
          (map? x) (Vec2. (get x :x 0.0) (get x :y 0.0) (meta x))
          :default (type-error "Vec2" x)))
    ([x y] (Vec2. x y nil)))
  
  (defn vec3
    ([] (Vec3. 0.0 0.0 0.0 nil))
    ([x] (cond
          (instance? Vec3 x) x
          (sequential? x) (Vec3. (nth x 0 0.0) (nth x 1 0.0) (nth x 2 0.0) (meta x))
          (number? x) (Vec3. x x x nil)
          (map? x) (Vec3. (get x :x 0.0) (get x :y 0.0) (get x :z 0.0) (meta x))
          :default (type-error "Vec3" x)))
    ([x y] (Vec3. x y 0.0 nil))
    ([x y z] (Vec3. x y z nil)))
  
  (defn vec2? [x] (instance? Vec2 x))
  (defn vec3? [x] (instance? Vec3 x))
#+END_SRC
** Random vectors
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  (defn randvec2
    ([] (normalize (vec2 (m/randnorm) (m/randnorm))))
    ([n] (normalize (vec2 (m/randnorm) (m/randnorm)) n)))

  (defn randvec3
    ([] (normalize (vec3 (m/randnorm) (m/randnorm) (m/randnorm))))
    ([n] (normalize (vec3 (m/randnorm) (m/randnorm) (m/randnorm)) n)))
#+END_SRC
** Swizzling
#+BEGIN_SRC clojure :tangle babel/src-cljx/thi/ng/geom/core/vec.cljx
  (defn swizzle2
    [^Vec2 _ k default]
    (if (number? k)
      (case (int k)
        0 (.-x _)
        1 (.-y _)
        (or default (key-error k)))
      (case k
        :x (.-x _)
        :y (.-y _)
        (let [n (name k) c (count n)]
          (case c
            2 (->> n
                   (map #(case % \x (.-x _) \y (.-y _) (or default (key-error k))))
                   ((fn [[x y]] (Vec2. x y (.-_meta _)))))
            3 (->> n
                   (map #(case % \x (.-x _) \y (.-y _) (or default (key-error k))))
                   ((fn [[x y z]] (Vec3. x y z (.-_meta _)))))
            (or default (key-error k)))))))

  (defn swizzle3
    [^Vec3 _ k default]
    (if (number? k)
      (case (int k)
        0 (.-x _)
        1 (.-y _)
        2 (.-z _)
        (or default (key-error k)))
      (case k
        :x (.-x _)
        :y (.-y _)
        :z (.-z _)
        (let [n (name k) c (count n)]
          (case c
            2 (->> n
                   (map #(case % \x (.-x _) \y (.-y _) \z (.-z _) (or default (key-error k))))
                   ((fn [[x y]] (Vec2. x y (.-_meta _)))))
            3 (->> n
                   (map #(case % \x (.-x _) \y (.-y _) \z (.-z _) (or default (key-error k))))
                   ((fn [[x y z]] (Vec3. x y z (.-_meta _)))))
            (or default (key-error k)))))))
#+END_SRC
